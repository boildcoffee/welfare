// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: "ext.gradle"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects{
    print(it.name)
    def isAppModule = it.name.equals('app')
    def enableDataBinding = isAppModule || it.name.equals('databinding') || it.name.equals("library-base")
    apply plugin: isAppModule? 'com.android.application': 'com.android.library'

    android {
        compileSdkVersion _app.compileSdkVersion as Integer
        defaultConfig {
            if(isAppModule){
                applicationId "com.boiledcoffee.welfare"
                multiDexEnabled true
            }
            minSdkVersion _app.minSdkVersion as Integer
            targetSdkVersion _app.targetSdkVersion as Integer
            versionCode _app.versionCode as Integer
            versionName _app.versionCode as String
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            buildConfigField "int", "dbVersion", "1"

            ndk {
                //设置支持的SO库架构
//            abiFilters 'armeabi' , 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
                abiFilters _app.abiFilters
            }
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                shrinkResources false
                zipAlignEnabled true
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        packagingOptions {
            exclude 'META-INF/rxjava.properties'
            exclude 'android/databinding/DataBinderMapper.java'
            exclude 'android/databinding/DataBindingComponent.java'
            exclude 'android/databinding/DynamicUtil.java'
        }

        if(enableDataBinding){
            dataBinding {
                enabled true
            }
        }
    }
}
